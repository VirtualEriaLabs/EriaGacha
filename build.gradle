plugins {
	id 'fabric-loom' version '0.9.+'
	id 'maven-publish'
	id 'org.springframework.boot' version '2.5.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'

}
sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	mavenCentral()
	maven {
		url "https://gitlab.com/api/v4/projects/25316865/packages/maven"
		name "GitLab"
		credentials(HttpHeaderCredentials) {
			name = 'Private-Token'
			value = project.git_lab_private_token
		}
		authentication {
			header(HttpHeaderAuthentication)
		}
	}
}



dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
	compileOnly 'org.projectlombok:lombok:1.18.20'
	annotationProcessor 'org.projectlombok:lombok:1.18.20'
	implementation 'dev.miku:r2dbc-mysql:0.8.2.RELEASE'
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc:2.5.3'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

configurations {
	all {
		exclude group: 'io.netty', module: 'netty-all'
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}
processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 16
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}
bootJar {
	enabled = false
}
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}
	repositories {
		maven {
			url "https://gitlab.com/api/v4/projects/25316865/packages/maven"
			credentials(HttpHeaderCredentials) {
				name = "PRIVATE-TOKEN"
				value = project.git_lab_private_token
			}
			authentication {
				header(HttpHeaderAuthentication)
			}
		}

	}
}
